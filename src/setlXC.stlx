load("util.stlx");
load("lexer.stlx");
load("parser.stlx");
load("imports.stlx");
load("ir.stlx");
load("transpile.stlx");

compile := procedure(file) {
	global.push("compile", [["file", file]]);
	str  := join(readFile(file), "\n");
	toks := lexer(file, str).lex();
	ast  := parser(file, toks).parse();
	ast  := addImports(file, ast)[1];
	if (global.debugLevel >= 2) {
		print("AST:"); // @Cleanup
		print(ast.dumpToStr(true));
	}
	ir   := codegenBlock(file, ast);
	if (global.debugLevel >= 2) {
		print("\nIR:"); // @Cleanup
		print(ir.displayStr());
	}

	c := irToC(file, ir);
	writeFile("../out.c", c); // @TODO: Provide parameter for name of output file
	global.pop();
};


main := procedure(params) {
	if (#params < 1) {
		printErr("Not enough arguments were provided.");
		printErr();
		printErr("Usage:");
		printErr("setlX setlXC.stlx -p <filename> [-ddd]");
		printErr("The flag '-d'   is optional and enables debugging mode.      It prints some useful debugging information.");
		printErr("The flag '-dd'  is optional and enables extended debugging mode. It prints alongside the usual debug info also the AST, IR and the current stack trace in case of an error.");
		printErr("The flag '-ddd' is optional and enables full debugging mode. It prints the usual debug info, the AST, IR and the entire call tree of the compiler in case of an error.");
		return;
	}

	if (#params >= 2 && params[2][..2] == "-d") {
		global.debugLevel := 1;
		i := 3;
		while (params[2][i] == "d") {
			global.debugLevel += 1;
			i += 1;
		}
	}

	global.push("main", [["params", params]]);
	try {
		compile(params[1]);
	} catch (e) {
		printErr(e);
		if (global.debugLevel >= 2) {
			printErr("\nStack:");
			if (global.debugLevel >= 3) {
				printErr(global.st.toFullStr());
			} else {
				printErr(global.st.toStr());
			}
		}
	}
	global.pop();
};

main(params);